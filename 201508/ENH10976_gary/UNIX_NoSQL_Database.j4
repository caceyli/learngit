fingerprint com.bdna.modules.app.Oracle.UNIX_NoSQL_Database "Oracle NoSQL Database on UNIX" {
    metadata {
        version 1;
        compatibleWith 1;
        newCategory "UNIX_NoSQL_Database:Oracle NoSQL Database on UNIX";
        category "Oracle";
        depends "com.bdna.modules.os.UNIX 1";
    }

    xml moduleDependency_UNIX_NoSQL_Database {:
        <moduleDependency
            parentModuleName="com.bdna.modules.app.Oracle" parentModuleVersion="1" />
    :}

    type UNIX_NoSQL_Database
        extends root.types.resource.software.application.Oracle
        "Oracle NoSQL Database on UNIX" {
        edition String "Edition";
    }

    relationship UNIX, UNIX_NoSQL_Database;

    code UNIX {
        List $$ceInstallDirs = findFiles(new List("/lib/kvstore.jar", "/lib/kvclient.jar", "/lib/kvcli.jar"));
        List $$eeInstallDirs = findFiles(new List("/lib/kvstore.jar","/lib/kvstore-ee.jar", "/lib/nosql.mib"));
        $$eeInstallDirs = push($$eeInstallDirs,"TAG");
    }
    code UNIX {
        String $isEE = "False";
        foreach String $eedir ($$eeInstallDirs) {
            if (!contains($eedir, "TAG")) {
                $isEE = "True";
                create UNIX_NoSQL_Database (
                    elementName         => concat("UNIX_NoSQL_Database_",$eedir),
                    name                => "Oracle NoSQL Database on UNIX",
                    installDirectory    => $eedir,
                    edition             => "Enterprise"
                );
            }
        }
        if (equals($isEE, "False")) {
            foreach String $cedir ($$ceInstallDirs) {
                    create UNIX_NoSQL_Database (
                        elementName         => concat("UNIX_NoSQL_Database_",$cedir),
                        name                => "Oracle NoSQL Database on UNIX",
                        installDirectory    => $cedir,
                        edition             => "Community"
                    );
            }
        }
    }

    code UNIX_NoSQL_Database {
        Map $env = new Map();
        List $versionInfo = executeCommand(
                                    $env,
                                    $$installDirectory,
                                    concat("java -jar ",concat($$installDirectory, "/lib/kvstore.jar version")),
                                    true);

        Regexp $exVersion = new Regexp("\\w+[Rr]\\d\\.(\\d[\\d\\.]*)\\s*");
        String $version = "";
        foreach String $line ($versionInfo) {
            if ((!defined($version)) && regexpIsMatch($line, $exVersion)) {
                $version = (String)lref(regexpFirstMatch($line, $exVersion), 0);
            }
        }
        $$UNIX_NoSQL_Database.version = $version;
    }

    code UNIX_NoSQL_Database {
        List $runningProcess = findProcess(new List("/lib/kvstore.jar start"));
        String $status = "False";
        foreach String $proc($runningProcess) {
            if (defined($proc) && regexpIsMatch($proc, new Regexp($$UNIX_NoSQL_Database.installDirectory))) {
                $status = "True";
            }
        }
        $$UNIX_NoSQL_Database.isRunning = $status;
    }
}


